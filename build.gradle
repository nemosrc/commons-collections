plugins {
    id 'java'
    id 'maven-publish'
    id 'net.saliman.cobertura' version '2.3.1'
    id 'com.github.kt3k.coveralls' version '2.8.2'
}

cobertura.coverageFormats = ['html', 'xml']

group 'com.nemosw.commons'
version '0.0.2'

sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

publishing {
    publications {
        mavenCollection(MavenPublication) {
            artifactId = 'commons-collections'
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().appendNode('name', 'Nemo')
                asNode().appendNode('description', 'Nemo\'s commons-collections library')
                asNode().appendNode('url', 'https://github.com/nemosrc/commons-collections')
                asNode().appendNode('licenses').appendNode('license').
                        appendNode('name', 'MIT License').parent().
                        appendNode('url', 'http://www.opensource.org/licenses/mit-license.php')
                asNode().appendNode('developers').appendNode('developer').
                        appendNode('id', 'nemo').parent().
                        appendNode('name', 'Nemo').parent().
                        appendNode('email', 'nemosrc@gmail.com').parent().
                        appendNode('url', 'https://github.com/nemosrc')
                asNode().appendNode('scm').
                        appendNode('connection', 'scm:git:git://github.com/nemosrc/commons-collections.git').parent().
                        appendNode('developerConnection', 'scm:git:ssh://github.com/nemosrc/commons-collections.git').parent().
                        appendNode('url', 'https://github.com/nemosrc/commons-collections')
            }
        }
    }
}